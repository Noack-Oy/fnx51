                   
                   ; *********************************
                   ; * Serial Interface Test Program *
                   ; *********************************
                   
                   ; This program prints "Hello, world!",
                   ; then echoes every byte received,
                   ; but incremented by one
                   
                   
                   ; **********************
                   ; * Header Definitions *
                   ; **********************
                   
                   ; *** serial/sfr.equ ***
                   
                   ; Special function registers for serial interface.
                   ; See chapter 14 of AT89C51ED2 datasheet, page 48ff.
                   
                   ; Internal Baud Rate Generator (BRG), see datasheet page 52.
0000:                       .equ    BRL,    0x9A    ; baud rate reload value
0000:                       .equ    BDRCON, 0x9B    ; BRG control register
                   
                   
                   ; *************
                   ; * Main Code *
                   ; *************
                   
                   ; *** test/serial.asm ***
                   
0000:                       .org    0
                   
0000: 11 25                 acall   serial_init
                   
0002: 90 00 15              mov     dptr,   #hello_text
                   hello_loop:
0005: E4                    clr     a
0006: 93                    movc    a,      @dptr+a
0007: 60 05                 jz      hello_end
0009: 11 2F                 acall   serial_tx
000B: A3                    inc     dptr
000C: 80 F7                 sjmp    hello_loop
                   hello_end:
                   
                   echo_loop:
000E: 11 37                 acall   serial_rx
0010: 04                    inc     a
0011: 11 2F                 acall   serial_tx
0013: 80 F9                 sjmp    echo_loop
                   
                   hello_text:
0015: 48 65 6C 6C 
      6F 2C 20 77 
      6F 72 6C 64 
      21                    .byte   "Hello, world!"
0022: 0D 0A 00              .byte   13, 10, 0
                   
                   
                   ; *********************
                   ; * Library Functions *
                   ; *********************
                   
                   ; *** serial/init.inc ***
                   
                   ; >> void serial_init() <<
                   
                   ; Initialize serial interface.
                   ; - assumes 20MHz clock and X2 mode
                   ; - uses internal baud rate generator
                   ; - speed: 9600 bits per second
                   ; Formula: BRL = 256 - (F_clk / (32 * Baud_Rate))
                   ; ---
                   ; See datasheet page 52 for details,
                   ; also note oscillator diagram on page 16!
                   
                   serial_init:
0025: 75 98 52              mov     SCON,   #0x52   ; SM1, REN, TI
0028: 75 9A BF              mov     BRL,    #191    ; 9600 @20MHz CLK
002B: 75 9B 1E              mov     BDRCON, #0x1e   ; BRR, RBCK, TBCK, SPD
002E: 22                    ret
                   
                   ; *** serial/tx.inc ***
                   
                   ; >> void serial_tx(char a) <<
                   
                   ; Transmit byte from accumulator.
                   
                   serial_tx:
002F: 30 99 FD              jnb     TI,     serial_tx
0032: C2 99                 clr     TI
0034: F5 99                 mov     SBUF,   a
0036: 22                    ret
                   
                   ; *** serial/rx.inc ***
                   
                   ; >> char serial_rx <<
                   
                   ; Receive byte into accumulator.
                   
                   serial_rx:
0037: 30 98 FD              jnb     RI,     serial_rx
003A: C2 98                 clr     RI
003C: E5 99                 mov     a,      SBUF
003E: 22                    ret
