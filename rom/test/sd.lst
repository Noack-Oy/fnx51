                   
                   ; ************************
                   ; * SD Card Test Program *
                   ; ************************
                   
                   ; This program initializes an SD card
                   ; and dumps the first sector.
                   
                   
                   ; **********************
                   ; * Header Definitions *
                   ; **********************
                   
                   ; *** global/variables.equ ***
                   
                   ; variables in internal RAM
                   
                   ; 0x00-0x1f: register banks 0-3
                   ; 0x20-0x2f: bit addressable variables
                   
0000:              .equ    in,             0x30    ; pointer to input handling 
0000:              .equ    out,            0x32    ; pointer to ouput handling 
                   
0000:              .equ    stream_in,      0x34    ; input stream position
0000:              .equ    stream_out,     0x36    ; output stream position
                   
0000:              .equ    memory_list,    0x38    ; pointer to heap list head
0000:              .equ    memory_start,   0x40    ; first allocatable address
0000:              .equ    memory_end,     0x42    ; last allocatable address
                   
0000:              .equ    block_read,     0x44    ; pointers to block device d
0000:              .equ    block_write,    0x46    ; code (read and write funct
0000:              .equ    block_cache,    0x48    ; pointer to block cache dat
                   
0000:              .equ    fatfs_info,     0x50    ; pointer to FAT file system
                   
0000:              .equ    stack,          0x60    ; beginning of stack (grows 
                   
                   ; *** global/sfr.equ ***
                   
                   ; global SFRs
                   
0000:              .equ    auxr,   0x8e    ; auxiliary register 0
0000:              .equ    auxr1,  0xa2    ; auxiliary register 1
                   
                   ; *** serial/sfr.equ ***
                   
                   ; Special function registers for serial interface.
                   ; See chapter 14 of AT89C51ED2 datasheet, page 48ff.
                   
                   ; Internal Baud Rate Generator (BRG), see datasheet page 52.
0000:                      .equ    BRL,    0x9A    ; baud rate reload value
0000:                      .equ    BDRCON, 0x9B    ; BRG control register
                   
                   ; *** spi/sfr.equ ***
                   
                   ; Special function registers for serial peripheral interface
                   ; See chapter 16 of AT89C51ED2 datasheet
                   
0000:              .equ    spcon,  0xc3    ; Serial Peripheral Control Register
0000:              .equ    spsta,  0xc4    ; Serial Peripheral Status Register
0000:              .equ    spdat,  0xc5    ; Serial Peripheral Data Register
                   
                   ; *** sd/config.equ ***
                   
                   ; SPCON register configuration for SD card:
                   ; .-----.-------.-------.-----------------------------------
                   ; | bit | name  | value | remark                            
                   ; |-----|-------|-------|-----------------------------------
                   ; | 7   | SPR2  | 1     | SPR2:0 = 101-> Fclk_periph / 64   
                   ; | 6   | SPEN  | 1     | Set to enable the SPI             
                   ; | 5   | SSDIS | 1     | Set to disable /SS input          
                   ; | 4   | MSTR  | 1     | Set to configure the SPI as a mast
                   ; | 3   | CPOL  | 0     | Cleared to have SCK idle at '0'   
                   ; | 2   | CPHA  | 0     | Cleared to sample on leading edge 
                   ; | 1   | SPR1  | 0     | (see above)                       
                   ; | 0   | SPR0  | 1     | (see above)   speed: 312.5 kHz    
                   ; '-----'-------'-------'-----------------------------------
                   
0000:              .equ    sd_spi_config,  0xf1
                   
0000:              .flag   sd_pin_cs,      p1.1
                   
                   ; *************
                   ; * Main Code *
                   ; *************
                   
                   ; *** test/sd.asm ***
                   
0000:              .org    0
                   
                   ; *** global/init.inc ***
                   
                   ; Initialize stack pointer and global variables.
                   ; This must be included early in the main program.
                   
                   global_init:
0000: 75 81 60             mov     SP,#STACK
                   
                   ; Select register bank zero, clear processor flags
0003: 75 D0 00             mov     PSW,#0
                   
                   ; Fill internal RAM with markers for uninitialized data
0006: 78 00                mov     r0,#0 ; write pointer
                   
0008: 74 3F                mov     a,#0x3f ; '?' for uninitialized registers
                   global_init__1:
000A: 08                   inc     r0 ; first iteration skips address 0, r0 liv
000B: F6                   mov     @r0,a
000C: B8 1F FB             cjne    r0,#0x1f,global_init__1
                   
000F: 74 2A                mov     a,#0x2a ; '*' for uninitialized variables
                   global_init__2:
0011: 08                   inc     r0
0012: F6                   mov     @r0,a
0013: B8 5F FB             cjne    r0,#STACK-1,global_init__2
                   
0016: 74 7E                mov     a,#0x7e ; '~' for stack and above
                   global_init__3:
0018: 08                   inc     r0
0019: F6                   mov     @r0,a
001A: B8 FF FB             cjne    r0,#0xff,global_init__3
                   
001D: 31 00                acall   serial_init
001F: 31 37                acall   sd_init
0021: 51 CE                acall   print_hex_8     ; status
0023: 51 EE                acall   print_hex_32    ; operation condition
0025: 74 0D                mov     a,#13
0027: 51 C9                acall   print_char
0029: 74 0A                mov     a,#10
002B: 51 C9                acall   print_char
                   
                           ; use xram
002D: 53 8E E1             anl     auxr,#0xe1      ; clear extram, xrs0-2
0030: 43 8E 10             orl     auxr,#0x10      ; set xrs2 (size 1792 bytes)
                   
0033: E4                   clr     a
0034: F5 82                mov     dpl,a
0036: F5 83                mov     dph,a
0038: F8                   mov     r0,a
0039: F9                   mov     r1,a
003A: FA                   mov     r2,a
003B: FB                   mov     r3,a
003C: 51 57                acall   sd_block_read
                   
                           ; print CRC
003E: EC                   mov     a,r4
003F: F8                   mov     r0,a
0040: ED                   mov     a,r5
0041: F9                   mov     r1,a
0042: 51 E3                acall   print_hex_16
0044: 74 0D                mov     a,#13
0046: 51 C9                acall   print_char
0048: 74 0A                mov     a,#10
004A: 51 C9                acall   print_char
                   
004C: E4                   clr     a
004D: F8                   mov     r0,a
004E: F9                   mov     r1,a
004F: FA                   mov     r2,a
0050: FB                   mov     r3,a
0051: F5 34                mov     stream_in,a
0053: F5 35                mov     stream_in+1,a
0055: 90 03 15             mov     dptr,#stream_xram_read
0058: 85 82 30             mov     in,dpl
005B: 85 83 31             mov     in+1,dph
005E: 71 48                acall   dump
0060: 71 48                acall   dump
                   
                   halt:
0062: 80 FE                sjmp    halt
                   
0100:              .org    0x0100
                   
                   ; *********************
                   ; * Library Functions *
                   ; *********************
                   
                   ; *** serial/init.inc ***
                   
                   ; >> void serial_init() <<
                   
                   ; Initialize serial interface.
                   ; - assumes 20MHz clock and X2 mode
                   ; - uses internal baud rate generator
                   ; - speed: 9600 bits per second
                   ; Formula: BRL = 256 - (F_clk / (32 * Baud_Rate))
                   ; ---
                   ; See datasheet page 52 for details,
                   ; also note oscillator diagram on page 16!
                   
                   serial_init:
0100: 75 98 52             mov     SCON,   #0x52   ; SM1, REN, TI
0103: 75 9A BF             mov     BRL,    #191    ; 9600 @20MHz CLK
0106: 75 9B 1E             mov     BDRCON, #0x1e   ; BRR, RBCK, TBCK, SPD
                   
0109: 90 01 24             mov     dptr,   #serial_rx
010C: 85 82 30             mov     IN,     DPL
010F: 85 83 31             mov     IN+1,   DPH
                   
0112: 90 01 1C             mov     dptr,   #serial_tx
0115: 85 82 32             mov     OUT,    DPL
0118: 85 83 33             mov     OUT+1,  DPH
                   
011B: 22                   ret
                   
                   ; *** serial/tx.inc ***
                   
                   ; >> void serial_tx(char a) <<
                   
                   ; Transmit byte from accumulator.
                   
                   serial_tx:
011C: 30 99 FD             jnb     TI,     serial_tx
011F: C2 99                clr     TI
0121: F5 99                mov     SBUF,   a
0123: 22                   ret
                   
                   ; *** serial/rx.inc ***
                   
                   ; >> char serial_rx <<
                   
                   ; Receive byte into accumulator.
                   
                   serial_rx:
0124: 30 98 FD             jnb     RI,     serial_rx
0127: C2 98                clr     RI
0129: E5 99                mov     a,      SBUF
012B: 22                   ret
                   
                   
                   ; *** spi/transfer.inc ***
                   
                   ; >> char{a} serial_transfer(char{a} data) <<
                   
                   ; SPI transfer function: send byte from a, read response int
                   
                   spi_transfer:
012C: F5 C5                mov     spdat,a
                   
                   spi_transfer__wait:
012E: E5 C4                mov     a,spsta
0130: 54 80                anl     a,#0x80 ;check SPIF bit
0132: 60 FA                jz      spi_transfer__wait
                   
0134: E5 C5                mov     a,spdat
0136: 22                   ret
                   
                   ; *** sd/init.inc ***
                   
                   ; >> uint8{a},uint32{r0-r3} sd_init() <<
                   
                   ; Initialize SD card, return error code in a, OCR in r0-r3.
                   sd_init:
0137: ED                   mov     a,r5    ; r5 is used as temp store for error
0138: C0 E0                push    acc
013A: C0 82                push    dpl
013C: C0 83                push    dph
                   
013E: 75 C3 F1             mov     spcon,#sd_spi_config
0141: 31 85                acall   sd_warmup
0143: 31 9A                acall   sd_select
                   
0145: 7D 05                mov     r5,#5   ; 5: no idle response to cmd0
0147: 31 A9                acall   sd_cmd0
0149: B4 01 31             cjne    a,#1,sd_init__end
                   
014C: 1D                   dec     r5      ; 4: no idle response to cmd8
014D: 31 B9                acall   sd_cmd8
014F: B4 01 2B             cjne    a,#1,sd_init__end
0152: 1D                   dec     r5      ; 3: invalid cmd8 response
0153: BB 00 27             cjne    r3,#0x00,sd_init__end
0156: BA 00 24             cjne    r2,#0x00,sd_init__end
0159: B9 01 21             cjne    r1,#0x01,sd_init__end
015C: B8 AA 1E             cjne    r0,#0xaa,sd_init__end
                   
015F: 7D 64                mov     r5,#100 ; retry counter
                   sd_init__loop:
0161: 31 D1                acall   sd_acmd41
0163: 60 0A                jz      sd_init__continue
0165: 74 0A                mov     a,#10
0167: 51 81                acall   delay_ms_8
0169: DD F6                djnz    r5,sd_init__loop
016B: 7D 02                mov     r5,#2   ; 2: acmd41 ready response timeout
016D: 80 0E                sjmp    sd_init__end
                   
                   sd_init__continue:
016F: 7D 01                mov     r5,#1   ; 1: no ready response to cmd58
0171: 31 F9                acall   sd_cmd58
                   
0173: 1D                   dec     r5      ; 0: success
                   
0174: 90 02 57             mov     dptr,#sd_block_read
0177: 85 82 44             mov     block_read,dpl
017A: 85 83 45             mov     block_read+1,dph
                   
                   sd_init__end:
017D: D0 83                pop     dph
017F: D0 82                pop     dpl
0181: D0 E0                pop     acc
0183: CD                   xch     a,r5
0184: 22                   ret
                   
                   ; *** sd/warmup.inc ***
                   
                   ; >> void sd_warmup() <<
                   
                   ; send 80 warmup pulses to sclk while cs is high
                   sd_warmup:
0185: C0 E0                push    acc
0187: C0 F0                push    b
0189: D2 91                setb    sd_pin_cs ; p1.1
018B: 75 F0 0A             mov     b,#10
                   sd_warmup_loop:
018E: 74 FF                mov     a,#0xff
0190: 31 2C                acall   spi_transfer
0192: D5 F0 F9             djnz    b,sd_warmup_loop
0195: D0 F0                pop     b
0197: D0 E0                pop     acc
0199: 22                   ret
                   
                   ; *** sd/select.inc ***
                   
                   ; >> void sd_select() <<
                   
                   ; Activate the SD card
                   sd_select:
019A: C0 E0                push    acc
019C: 74 FF                mov     a,#0xff
019E: 31 2C                acall   spi_transfer
01A0: C2 91                clr     sd_pin_cs
01A2: 74 FF                mov     a,#0xff
01A4: 31 2C                acall   spi_transfer
01A6: D0 E0                pop     acc
01A8: 22                   ret
                   
                   ; *** sd/cmd0.inc ***
                   
                   ; >> char{a} sd_cmd0() <<
                   
                   ; CMD0: go to idle state
                   sd_cmd0:
01A9: 51 9D                acall   regbank_next
01AB: E4                   clr     a
01AC: F8                   mov     r0,a
01AD: F9                   mov     r1,a
01AE: FA                   mov     r2,a
01AF: FB                   mov     r3,a
01B0: 7C 94                mov     r4,#0x94
01B2: 51 0D                acall   sd_command
01B4: 51 27                acall   sd_response1
01B6: 51 B6                acall   regbank_prev
01B8: 22                   ret
                   
                   ; *** sd/cmd8.inc ***
                   
                   ; >> uint8{a},uint32{r0-r3} sd_cmd8() <<
                   
                   ; CMD8: send interface condition
                   sd_cmd8:
01B9: EC                   mov     a,r4
01BA: C0 E0                push    acc
01BC: 74 08                mov     a,#8
01BE: 78 AA                mov     r0,#0xaa
01C0: 79 01                mov     r1,#0x01
01C2: 7A 00                mov     r2,#0x00
01C4: 7B 00                mov     r3,#0x00
01C6: 7C 86                mov     r4,#0x86
01C8: 51 0D                acall   sd_command
01CA: 51 39                acall   sd_response7
01CC: FC                   mov     r4,a
01CD: D0 E0                pop     acc
01CF: CC                   xch     a,r4
01D0: 22                   ret
                   
                   ; *** sd/acmd41.inc ***
                   
                   ; >> char{a} sd_acmd41() <<
                   
                   ; ACMD41: send operation condition
                   sd_acmd41:
01D1: 31 E8                acall   sd_cmd55
01D3: B4 01 11             cjne    a,#1,sd_acmd41__end
01D6: 51 9D                acall   regbank_next
01D8: E4                   clr     a
01D9: F8                   mov     r0,a
01DA: F9                   mov     r1,a
01DB: FA                   mov     r2,a
01DC: 7B 40                mov     r3,#0x40 ; indicates high capacity support
01DE: FC                   mov     r4,a
01DF: 74 29                mov     a,#41
01E1: 51 0D                acall   sd_command
01E3: 51 27                acall   sd_response1
01E5: 51 B6                acall   regbank_prev
                   sd_acmd41__end:
01E7: 22                   ret
                   
                   ; *** sd/cmd55.inc ***
                   
                   ; >> char{a} sd_cmd55() <<
                   
                   ; CMD55: application specific command (prefix)
                   sd_cmd55:
01E8: 51 9D                acall   regbank_next
01EA: E4                   clr     a
01EB: F8                   mov     r0,a
01EC: F9                   mov     r1,a
01ED: FA                   mov     r2,a
01EE: FB                   mov     r3,a
01EF: FC                   mov     r4,a
01F0: 74 37                mov     a,#55
01F2: 51 0D                acall   sd_command
01F4: 51 27                acall   sd_response1
01F6: 51 B6                acall   regbank_prev
01F8: 22                   ret
                   
                   ; *** sd/cmd58.inc ***
                   
                   ; >> uint8{a},uint32{r0-r3} sd_cmd58() <<
                   
                   ; CMD58: read operation conditions register
                   sd_cmd58:
01F9: EC                   mov     a,r4
01FA: C0 E0                push    acc
01FC: E4                   clr     a
01FD: F8                   mov     r0,a
01FE: F9                   mov     r1,a
01FF: FA                   mov     r2,a
0200: FB                   mov     r3,a
0201: FC                   mov     r4,a
0202: 74 3A                mov     a,#58
0204: 51 0D                acall   sd_command
0206: 51 39                acall   sd_response3
0208: FC                   mov     r4,a
0209: D0 E0                pop     acc
020B: CC                   xch     a,r4
020C: 22                   ret
                   
                   ; *** sd/command.inc ***
                   
                   ; >> void sd_command(uint8{a} cmd, uint32{r0-r3} arg, uint8{
                   
                   ; Issue a command to the SD card
                   sd_command:
020D: C0 E0                push    acc
                           ; transmit command
020F: 44 40                orl     a,#0x40 ; transmission bit
0211: 31 2C                acall   spi_transfer
                           ; transmit argument
0213: EB                   mov     a,r3
0214: 31 2C                acall   spi_transfer
0216: EA                   mov     a,r2
0217: 31 2C                acall   spi_transfer
0219: E9                   mov     a,r1
021A: 31 2C                acall   spi_transfer
021C: E8                   mov     a,r0
021D: 31 2C                acall   spi_transfer
                           ; transmit crc
021F: EC                   mov     a,r4
0220: 44 01                orl     a,#0x01 ; end bit
0222: 31 2C                acall   spi_transfer
0224: D0 E0                pop     acc
0226: 22                   ret
                   
                   ; *** sd/response.inc ***
                   
                   ; >> uint8{a} sd_response1() <<
                   
                   ; Read response from SD card in 'R1' format into a
                   sd_response1:
0227: C0 F0                push    b
0229: 85 08 F0             mov     b,8     ; number of tries
                   sd_response1__loop:
022C: 74 FF                mov     a,#0xff
022E: 31 2C                acall   spi_transfer
0230: B4 FF 03             cjne    a,#0xff,sd_response1__end       ; success
0233: D5 F0 F6             djnz    b,sd_response1__loop    ; retry / timeout
                   sd_response1__end:
0236: D0 F0                pop     b
0238: 22                   ret
                   
                   ; >> uint8{a}, uint32{r0-r3} sd_response3() <<
                   
                   ; Read response from SD card in 'R3' format into a and r0-r3
                   sd_response3: ; same as R7
                   
                   ; >> uint8{a}, uint32{r0-r3} sd_response7() <<
                   
                   ; Read response from SD card in 'R7' format into a and r0-r3
                   sd_response7:
0239: 51 27                acall   sd_response1
023B: B4 02 00             cjne    a,#0x02,sd_response7__1
                   sd_response7__1:
023E: 40 01                jc      sd_response7__2
0240: 22                   ret     ; error in R1 response
                   sd_response7__2:
0241: F8                   mov     r0,a
0242: 74 FF                mov     a,#0xff
0244: 31 2C                acall   spi_transfer
0246: FB                   mov     r3,a
0247: 74 FF                mov     a,#0xff
0249: 31 2C                acall   spi_transfer
024B: FA                   mov     r2,a
024C: 74 FF                mov     a,#0xff
024E: 31 2C                acall   spi_transfer
0250: F9                   mov     r1,a
0251: 74 FF                mov     a,#0xff
0253: 31 2C                acall   spi_transfer
0255: C8                   xch     a,r0
0256: 22                   ret
                   
                   ; *** sd/block_read.inc ***
                   
                   ; >> uint16{r4-r5} sd_block_read(
                   ;       uint32{r0-r3} block_address,
                   ;       char *{dptr}&destination) <<
                   
                   ; Read block at address from sd card, write to RAM at dptr.
                   ; Return CRC in r4-r5, block address unchanged,
                   ; dptr pointing to first byte after read block.
                   
                   sd_block_read:
0257: C0 E0                push    acc
                   
                           ; send CMD17 (single block read)
0259: 74 11                mov     a,#17
025B: 7C FF                mov     r4,#0xff ; command CRC, ignored
025D: 51 0D                acall   sd_command
                   sd_block_read__wait:
025F: 51 27                acall   sd_response1
0261: B4 FE FB             cjne    a,#0xfe,sd_block_read__wait
0264: 7D 00                mov     r5,#0 ; loop counter
                           ; this loop will run 256 times,
                           ; two bytes are read per iteration
                   sd_block_read__loop:
0266: 74 FF                mov     a,#0xff
0268: 31 2C                acall   spi_transfer
026A: F0                   movx    @dptr,a
026B: A3                   inc     dptr
026C: 74 FF                mov     a,#0xff
026E: 31 2C                acall   spi_transfer
0270: F0                   movx    @dptr,a
0271: A3                   inc     dptr
0272: DD F2                djnz    r5,sd_block_read__loop
                           ; read block CRC
0274: 74 FF                mov     a,#0xff
0276: 31 2C                acall   spi_transfer
0278: FD                   mov     r5,a
0279: 74 FF                mov     a,#0xff
027B: 31 2C                acall   spi_transfer
027D: FC                   mov     r4,a
                   
027E: D0 E0                pop     acc
0280: 22                   ret
                   
                   
                   ; *** util/delay.inc ***
                   
                   ; >> void delay_ms_8(uint8{a} duration) <<
                   
                   ; 1-256 millisecond delay, duration given in a.
                   ; Assumes 20MHz clock and X2 mode.
                   ; Actual delay is slightly longer.
                   delay_ms_8:
                   
0281: C0 E0                push    acc
0283: C0 F0                push    b
                   
                           ; This loop is repeated a times (256 if a is zero).
                           ; The goal is to stretch each repeat to one millisec
                   delay_ms_8__loop1:
                           ; Each millisecond is 20k clock periods.
                           ; In X2 mode, a machine cycle is 6 clock periods.
                           ; Therefore, a millisecond is approx. 3334 machine c
0285: 75 F0 B9             mov     b,#185  ; n = 185       cycles:   2
                   delay_ms_8__loop2:
0288: C0 E0                push    acc                     ; n*2 = 370
028A: C0 F0                push    b                       ; n*2 = 370
028C: A4                   mul     ab                      ; n*4 = 740
028D: A4                   mul     ab                      ; n*4 = 740
028E: D0 F0                pop     b                       ; n*2 = 370
0290: D0 E0                pop     acc                     ; n*2 = 370
0292: D5 F0 F3             djnz    b,delay_ms_8__loop2     ; n*2 = 370
0295: D5 E0 ED             djnz    acc,delay_ms_8__loop1   ;         2
                                                           ; Sum: 3334
0298: D0 F0                pop     b
029A: D0 E0                pop     acc
029C: 22                   ret
                   
                   ; *** util/regbank.inc ***
                   
                   ; >> void* regbank_next() <<
                   
                   ; Switch to next register bank.
                   ; Return pointer to r0 of old bank in new r0.
                   
                   regbank_next:
029D: C0 E0                push    acc
                           ; check if already at highest bank
029F: E5 D0                mov     a,      psw
02A1: 54 18                anl     a,      #0x18
02A3: B4 18 02             cjne    a,      #0x18,  regbank_next__continue
02A6: 91 04                acall   panic   ; overflow
                   regbank_next__continue:
                           ; switch to next bank
02A8: E5 D0                mov     a,      psw
02AA: 24 08                add     a,      #0x08
02AC: F5 D0                mov     psw,    a
                           ; calculate pointer to old bank
02AE: 54 18                anl     a,      #0x18
02B0: 24 F8                add     a,      #0xf8
02B2: F8                   mov     r0,     a
                           ; done
02B3: D0 E0                pop     acc
02B5: 22                   ret
                   
                   ; >> void regbank_prev() <<
                   
                   ; Switch to previous register bank.
                   
                   regbank_prev:
02B6: C0 E0                push    acc
                           ; check if already at lowest bank
02B8: E5 D0                mov     a,      psw
02BA: 54 18                anl     a,      #0x18
02BC: 70 02                jnz     regbank_prev__continue
02BE: 91 04                acall   panic   ; underflow
                   regbank_prev__continue:
                           ; switch to previous bank
02C0: E5 D0                mov     a,      psw
02C2: 24 F8                add     a,      #0xf8
02C4: F5 D0                mov     psw,    a
                           ; done
02C6: D0 E0                pop     acc
02C8: 22                   ret
                   
                   ; *** print/char.inc ***
                   
                   ; >> void print_char(char a) <<
                   
                   ; Print char from accumulator.
                   ; Indirect call to function behind OUT pointer.
                   
                   print_char:
02C9: C0 32                push    OUT
02CB: C0 33                push    OUT+1
02CD: 22                   ret
                   
                   ; *** print/hex.inc ***
                   
                   ; >> void print_hex_8(char a) <<
                   
                   ; Print hexadecimal number from accumulator.
                   
                   print_hex_8:
02CE: C4                   swap    a       ; high nibble first
02CF: 51 D2                acall   print_hex_8__nibble
02D1: C4                   swap    a
                   print_hex_8__nibble:
02D2: C0 E0                push    ACC
02D4: 54 0F                anl     a,      #0x0f
02D6: 24 90                add     a,      #0x90
02D8: D4                   da      a
02D9: 34 40                addc    a,      #0x40
02DB: D4                   da      a
02DC: 44 20                orl     a,      #0x20   ; lower case
02DE: 51 C9                acall   print_char
02E0: D0 E0                pop     ACC
02E2: 22                   ret
                   
                   ; >> void print_hex_16(int r0r1) <<
                   
                   ; Print hexadecimal number from r0-r1.
                   
                   print_hex_16:
02E3: C0 E0                push    ACC
02E5: E9                   mov     a,      r1      ; MSB first
02E6: 51 CE                acall   print_hex_8
02E8: E8                   mov     a,      r0      ; LSB second
02E9: 51 CE                acall   print_hex_8
02EB: D0 E0                pop     ACC
02ED: 22                   ret
                   
                   ; >> void print_hex_32(long r0r1r2r3) <<
                   
                   ; Print hexadecimal number from r0-r3.
                   
                   print_hex_32:
02EE: C0 E0                push    ACC
02F0: EB                   mov     a,      r3      ; MSB first
02F1: 51 CE                acall   print_hex_8
02F3: EA                   mov     a,      r2
02F4: 51 CE                acall   print_hex_8
02F6: E9                   mov     a,      r1
02F7: 51 CE                acall   print_hex_8
02F9: E8                   mov     a,      r0      ; LSB last
02FA: 51 CE                acall   print_hex_8
02FC: D0 E0                pop     ACC
02FE: 22                   ret
                   
                   ; *** print/text.inc ***
                   
                   ; >> void print_text(char* dptr) <<
                   
                   ; Print null-terminated string from program code.
                   ; Aborts if no null terminator is found after 256 bytes.
                   
                   print_text:
02FF: C0 E0                push    ACC
0301: E8                   mov     a,      r0
0302: C0 E0                push    ACC
                   
0304: E4                   clr     a
0305: F8                   mov     r0,     a
                   print_text__loop:
0306: 93                   movc    a,      @dptr+a
0307: 60 06                jz      print_text__end
0309: 51 C9                acall   print_char
030B: 08                   inc     r0
030C: E8                   mov     a,      r0
030D: 70 F7                jnz     print_text__loop
                   
                   print_text__end:
030F: D0 E0                pop     ACC
0311: F8                   mov     r0,     a
0312: D0 E0                pop     ACC
0314: 22                   ret
                   
                   ; *** stream/xram_read.inc ***
                   
                   ; >> char{a} stream_xram_read() <<
                   
                   ; Read byte from on-chip expanded RAM at stream input pointe
                   ; 16 bit value of the stream pointer is used and incremented
                   
                   stream_xram_read:
0315: C0 8E                push    auxr
0317: C0 82                push    dpl
0319: C0 83                push    dph
                   
031B: 53 8E E1             anl     auxr,#0xe1      ; clear extram, xrs0-2
031E: 43 8E 10             orl     auxr,#0x10      ; set xrs2 (size 1792 bytes)
                   
0321: 85 34 82             mov     dpl,stream_in
0324: 85 35 83             mov     dph,stream_in+1
                   
0327: E0                   movx    a,@dptr
0328: A3                   inc     dptr
0329: 85 82 34             mov     stream_in,dpl
032C: 85 83 35             mov     stream_in+1,dph
                   
032F: D0 83                pop     dph
0331: D0 82                pop     dpl
0333: D0 8E                pop     auxr
0335: 22                   ret
                   
                   ; *** read/char.inc ***
                   
                   ; >> char read_char() <<
                   
                   ; Read char into accumulator.
                   ; Indirect call to function behind IN pointer.
                   
                   read_char:
0336: C0 30                push    IN
0338: C0 31                push    IN+1
033A: 22                   ret
                   
                   ; *** util/xch.inc ***
                   
                   ; >> void xch_r0123_r4567(uint32 &r0r1r2r3, uint32 &r4r5r6r7
                   
                   ; Exchange registers r0-r3 with r4-r7.
                   
                   xch_r0123_r4567:
                           ; r0 <-> r4
033B: C8                   xch     a,      r0
033C: CC                   xch     a,      r4
033D: C8                   xch     a,      r0
                           ; r1 <-> r5
033E: C9                   xch     a,      r1
033F: CD                   xch     a,      r5
0340: C9                   xch     a,      r1
                           ; r2 <-> r6
0341: CA                   xch     a,      r2
0342: CE                   xch     a,      r6
0343: CA                   xch     a,      r2
                           ; r3 <-> r7
0344: CB                   xch     a,      r3
0345: CF                   xch     a,      r7
0346: CB                   xch     a,      r3
                           ; a remains unchanged
0347: 22                   ret
                   
                   ; *** util/dump.inc ***
                   
                   ; >> uint32{r0-r3} dump(uint32{r0-r3} addr, uint8{a} count) 
                   
                   ; Print hexdump from standard input to standard output.
                   ; Starting value for address column in r0-r3.
                   ; Number of bytes to print in a (0 means 256).
                   ; r0-r3 contains next address upon exit.
                   
                   dump:
0348: C0 F0                push    b
034A: C0 E0                push    acc
034C: F5 F0                mov     b,      a
034E: EC                   mov     a,      r4
034F: C0 E0                push    acc
0351: ED                   mov     a,      r5
0352: C0 E0                push    acc
0354: EE                   mov     a,      r6
0355: C0 E0                push    acc
0357: EF                   mov     a,      r7
0358: C0 E0                push    acc
                   
                           ; b: total bytes remaining
                           ; r0: buffer pointer
                           ; r1: read loop counter / padding size
                           ; r2: print loop counter
                   
                           ; r4-7: address label
035A: 71 3B                acall   xch_r0123_r4567
                   
                           ; allocate 16 byte array on stack
035C: E5 81                mov     a,      sp
035E: F8                   mov     r0,     a
035F: 24 10                add     a,      #16
0361: F5 81                mov     sp,     a
                   
                   dump__line_loop:
                           ; read into buffer
0363: 79 10                mov     r1,     #16
                   dump__read_loop:
0365: 71 36                acall   read_char
0367: 08                   inc     r0
0368: F6                   mov     @r0,    a
0369: D5 F0 03             djnz    b,      dump__read_continue
036C: 19                   dec     r1
036D: 80 02                sjmp    dump__read_end
                   dump__read_continue:
036F: D9 F4                djnz    r1,     dump__read_loop
                   dump__read_end:
                   
                           ; print address column
0371: 71 3B                acall   xch_r0123_r4567
0373: 51 EE                acall   print_hex_32
0375: 71 3B                acall   xch_r0123_r4567
0377: 74 3A                mov     a,      #':'
0379: 51 C9                acall   print_char
                   
                           ; if r1 is nonzero here, we have a shorter line
                           ; the value in r1 is the number of padding bytes
                   
                           ; wind back pointer
037B: E8                   mov     a,      r0
037C: 24 F0                add     a,      #-16
037E: 29                   add     a,      r1
037F: F8                   mov     r0,     a
                   
                           ; print hex
0380: 7A 10                mov     r2,     #16
                   dump__print_hex_loop:
0382: 08                   inc     r0
0383: EA                   mov     a,      r2
0384: 54 03                anl     a,      #3      ; mod 4
0386: 70 04                jnz     dump__print_hex_no_gap
0388: 74 20                mov     a,      #' ' ; gap after every 4 bytes
038A: 51 C9                acall   print_char
                   dump__print_hex_no_gap:
038C: EA                   mov     a,      r2
038D: D3                   setb    c
038E: 99                   subb    a,      r1
038F: 50 08                jnc     dump__print_hex_no_padding
0391: 74 20                mov     a,      #' ' ; padding for shorter line
0393: 51 C9                acall   print_char
0395: 51 C9                acall   print_char
0397: 80 03                sjmp    dump__print_hex_next
                   dump__print_hex_no_padding:
0399: E6                   mov     a,      @r0
039A: 51 CE                acall   print_hex_8
                   dump__print_hex_next:
039C: 74 20                mov     a,      #' ' ; space between bytes
039E: 51 C9                acall   print_char
03A0: DA E0                djnz    r2,     dump__print_hex_loop
                   
                           ; wind back again
03A2: E8                   mov     a,      r0
03A3: 24 F0                add     a,      #-16
03A5: F8                   mov     r0,     a
                   
                           ; print ascii
03A6: 74 20                mov     a,      #' '
03A8: 51 C9                acall   print_char
03AA: 74 7C                mov     a,      #'|'
03AC: 51 C9                acall   print_char
03AE: 74 10                mov     a,      #16
03B0: C3                   clr     c
03B1: 99                   subb    a,      r1
03B2: F9                   mov     r1,     a ; number of bytes to print
03B3: FA                   mov     r2,     a
                   dump__print_ascii_loop:
03B4: 08                   inc     r0
03B5: E6                   mov     a,      @r0
03B6: B4 20 00             cjne    a,      #0x20,  dump__print_ascii_check1
                   dump__print_ascii_check1:
03B9: 40 05                jc      dump__print_ascii_replace
03BB: B4 7F 00             cjne    a,      #0x7f,  dump__print_ascii_check2
                   dump__print_ascii_check2:
03BE: 40 02                jc      dump__print_ascii_continue
                   dump__print_ascii_replace:
03C0: 74 2E                mov     a,      #'.'
                   dump__print_ascii_continue:
03C2: 51 C9                acall   print_char
03C4: DA EE                djnz    r2,     dump__print_ascii_loop
03C6: 74 7C                mov     a,      #'|'
03C8: 51 C9                acall   print_char
03CA: 74 0D                mov     a,      #13
03CC: 51 C9                acall   print_char
03CE: 74 0A                mov     a,      #10
03D0: 51 C9                acall   print_char
                   
                           ; wind back again
03D2: E8                   mov     a,      r0
03D3: 24 F0                add     a,      #-16
03D5: F8                   mov     r0,     a
                   
                           ; next address
03D6: EC                   mov     a,      r4
03D7: 29                   add     a,      r1
03D8: FC                   mov     r4,     a
03D9: ED                   mov     a,      r5
03DA: 34 00                addc    a,      #0
03DC: FD                   mov     r5,     a
03DD: EE                   mov     a,      r6
03DE: 34 00                addc    a,      #0
03E0: FE                   mov     r6,     a
03E1: EF                   mov     a,      r7
03E2: 34 00                addc    a,      #0
03E4: FF                   mov     r7,     a
                   
03E5: E5 F0                mov     a,      b
03E7: 60 02                jz      dump__end
03E9: 61 63                ajmp    dump__line_loop
                   
                   dump__end:
                           ; deallocate array from stack
03EB: E5 81                mov     a,      sp
03ED: 24 F0                add     a,      #-16
03EF: F5 81                mov     sp,     a
                   
03F1: 71 3B                acall   xch_r0123_r4567
                   
03F3: D0 E0                pop     acc
03F5: FF                   mov     r7,     a
03F6: D0 E0                pop     acc
03F8: FE                   mov     r6,     a
03F9: D0 E0                pop     acc
03FB: FD                   mov     r5,     a
03FC: D0 E0                pop     acc
03FE: FC                   mov     r4,     a
03FF: D0 E0                pop     acc
0401: D0 F0                pop     b
0403: 22                   ret
                   
0404:              .equ panic_out, serial_tx
                   
                   ; *** panic/panic.inc ***
                   
                   ; >> void panic() <<
                   
                   ; Unrecoverable error handler.
                   ; Print caller address and halt.
                   
                   panic:
0404: C2 AF                clr     ea      ; disable interrupts
0406: F8                   mov     r0,a
0407: 90 01 1C             mov     dptr,#panic_out
040A: 85 82 32             mov     out,dpl
040D: 85 83 33             mov     out+1,dph
0410: 90 04 2E             mov     dptr,#panic_text
0413: 51 FF                acall   print_text
0415: D0 E0                pop     acc
0417: 51 CE                acall   print_hex_8
0419: D0 E0                pop     acc
041B: 51 CE                acall   print_hex_8
041D: 74 20                mov     a,#' '
041F: 51 C9                acall   print_char
0421: E8                   mov     a,r0
0422: 51 CE                acall   print_hex_8
0424: 74 0D                mov     a,#13
0426: 51 C9                acall   print_char
0428: 74 0A                mov     a,#10
042A: 51 C9                acall   print_char
042C: 80 FE                sjmp    *
                   
                   panic_text:
042E: 0D 0A                .db     13, 10
0430: 20 21 21 21 
      20 70 61 6E 
      69 63 20 40 
                           .db     " !!! panic @"
043C: 00                   .db     0
