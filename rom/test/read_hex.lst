                   
                   ; ************************************
                   ; * Hex Integer Parsing Test Program *
                   ; ************************************
                   
                   ; Parses and prints back hex numbers in a loop.
                   
                   
                   ; **********************
                   ; * Header Definitions *
                   ; **********************
                   
                   ; *** global/variables.equ ***
                   
                   ; variables in internal RAM
                   
                   ; 0x00-0x1f: register banks 0-3
                   ; 0x20-0x2f: bit addressable variables
                   
0000:              .equ    in,             0x30    ; pointer to input handling 
0000:              .equ    out,            0x32    ; pointer to ouput handling 
                   
0000:              .equ    stream_in,      0x34    ; input stream position
0000:              .equ    stream_out,     0x36    ; output stream position
                   
0000:              .equ    memory_list,    0x38    ; pointer to heap list head
0000:              .equ    memory_start,   0x40    ; first allocatable address
0000:              .equ    memory_end,     0x42    ; last allocatable address
                   
0000:              .equ    block_read,     0x44    ; pointers to block device d
0000:              .equ    block_write,    0x46    ; code (read and write funct
0000:              .equ    block_cache,    0x48    ; pointer to block cache dat
                   
0000:              .equ    fatfs_info,     0x50    ; pointer to FAT file system
                   
0000:              .equ    stack,          0x60    ; beginning of stack (grows 
                   
                   ; *** serial/sfr.equ ***
                   
                   ; Special function registers for serial interface.
                   ; See chapter 14 of AT89C51ED2 datasheet, page 48ff.
                   
                   ; Internal Baud Rate Generator (BRG), see datasheet page 52.
0000:                      .equ    BRL,    0x9A    ; baud rate reload value
0000:                      .equ    BDRCON, 0x9B    ; BRG control register
                   
                   
                   ; *************
                   ; * Main Code *
                   ; *************
                   
0000:              .org 0
                   
                   ; *** global/init.inc ***
                   
                   ; Initialize stack pointer and global variables.
                   ; This must be included early in the main program.
                   
                   global_init:
0000: 75 81 60             mov     SP,#STACK
                   
                   ; Select register bank zero, clear processor flags
0003: 75 D0 00             mov     PSW,#0
                   
                   ; Fill internal RAM with markers for uninitialized data
0006: 78 00                mov     r0,#0 ; write pointer
                   
0008: 74 3F                mov     a,#0x3f ; '?' for uninitialized registers
                   global_init__1:
000A: 08                   inc     r0 ; first iteration skips address 0, r0 liv
000B: F6                   mov     @r0,a
000C: B8 1F FB             cjne    r0,#0x1f,global_init__1
                   
000F: 74 2A                mov     a,#0x2a ; '*' for uninitialized variables
                   global_init__2:
0011: 08                   inc     r0
0012: F6                   mov     @r0,a
0013: B8 5F FB             cjne    r0,#STACK-1,global_init__2
                   
0016: 74 7E                mov     a,#0x7e ; '~' for stack and above
                   global_init__3:
0018: 08                   inc     r0
0019: F6                   mov     @r0,a
001A: B8 FF FB             cjne    r0,#0xff,global_init__3
                   
                   ; *** test/read_int_hex.asm ***
                   
001D: 11 25                acall   serial_init
                   
                   loop:
001F: 11 56                acall   read_hex_32
0021: 11 DA                acall   print_hex_32
0023: 80 FA                sjmp    loop
                   
                   
                   ; *********************
                   ; * Library Functions *
                   ; *********************
                   
                   ; *** serial/init.inc ***
                   
                   ; >> void serial_init() <<
                   
                   ; Initialize serial interface.
                   ; - assumes 20MHz clock and X2 mode
                   ; - uses internal baud rate generator
                   ; - speed: 9600 bits per second
                   ; Formula: BRL = 256 - (F_clk / (32 * Baud_Rate))
                   ; ---
                   ; See datasheet page 52 for details,
                   ; also note oscillator diagram on page 16!
                   
                   serial_init:
0025: 75 98 52             mov     SCON,   #0x52   ; SM1, REN, TI
0028: 75 9A BF             mov     BRL,    #191    ; 9600 @20MHz CLK
002B: 75 9B 1E             mov     BDRCON, #0x1e   ; BRR, RBCK, TBCK, SPD
                   
002E: 90 00 49             mov     dptr,   #serial_rx
0031: 85 82 30             mov     IN,     DPL
0034: 85 83 31             mov     IN+1,   DPH
                   
0037: 90 00 41             mov     dptr,   #serial_tx
003A: 85 82 32             mov     OUT,    DPL
003D: 85 83 33             mov     OUT+1,  DPH
                   
0040: 22                   ret
                   
                   ; *** serial/tx.inc ***
                   
                   ; >> void serial_tx(char a) <<
                   
                   ; Transmit byte from accumulator.
                   
                   serial_tx:
0041: 30 99 FD             jnb     TI,     serial_tx
0044: C2 99                clr     TI
0046: F5 99                mov     SBUF,   a
0048: 22                   ret
                   
                   ; *** serial/rx.inc ***
                   
                   ; >> char serial_rx <<
                   
                   ; Receive byte into accumulator.
                   
                   serial_rx:
0049: 30 98 FD             jnb     RI,     serial_rx
004C: C2 98                clr     RI
004E: E5 99                mov     a,      SBUF
0050: 22                   ret
                   
                   ; *** read/char.inc ***
                   
                   ; >> char read_char() <<
                   
                   ; Read char into accumulator.
                   ; Indirect call to function behind IN pointer.
                   
                   read_char:
0051: C0 30                push    IN
0053: C0 31                push    IN+1
0055: 22                   ret
                   
                   ; *** read/char.inc ***
                   
                   ; >> uint32 read_hex_32() <<
                   
                   ; Read 32 bit hex integer from input into r0-r3.
                   ; Stops at the first non-hex character or integer overflow.
                   
                   read_hex_32:
0056: C0 E0                push    acc
0058: EC                   mov     a,      r4
0059: C0 E0                push    acc
005B: ED                   mov     a,      r5
005C: C0 E0                push    acc
                   
005E: E4                   clr     a
005F: F8                   mov     r0,     a
0060: F9                   mov     r1,     a
0061: FA                   mov     r2,     a
0062: FB                   mov     r3,     a
                   
                   read_hex_32__loop:
0063: 11 51                acall   read_char
                   
0065: C3                   clr     c
0066: 94 30                subb    a,      #0x30   ; ascii '0'
0068: 40 42                jc      read_hex_32__end
006A: B4 0A 00             cjne    a,      #10,    read_hex_32__1
                   read_hex_32__1:
006D: 40 0D                jc      read_hex_32__3
006F: 94 11                subb    a,      #17     ; chars between '0' and 'A'
0071: 40 39                jc      read_hex_32__end
0073: 24 0A                add     a,      #10
0075: 54 DF                anl     a,      #0xdf   ; allow lowercase
0077: B4 10 00             cjne    a,      #0x10,  read_hex_32__2
                   read_hex_32__2:
007A: 50 30                jnc     read_hex_32__end
                   
                   read_hex_32__3:
007C: FC                   mov     r4,     a
007D: E8                   mov     a,      r0
007E: C4                   swap    a
007F: FD                   mov     r5,     a
0080: 54 F0                anl     a,      #0xf0
0082: 4C                   orl     a,      r4
0083: F8                   mov     r0,     a
0084: ED                   mov     a,      r5
0085: 54 0F                anl     a,      #0x0f
                   
0087: FC                   mov     r4,     a
0088: E9                   mov     a,      r1
0089: C4                   swap    a
008A: FD                   mov     r5,     a
008B: 54 F0                anl     a,      #0xf0
008D: 4C                   orl     a,      r4
008E: F9                   mov     r1,     a
008F: ED                   mov     a,      r5
0090: 54 0F                anl     a,      #0x0f
                   
0092: FC                   mov     r4,     a
0093: EA                   mov     a,      r2
0094: C4                   swap    a
0095: FD                   mov     r5,     a
0096: 54 F0                anl     a,      #0xf0
0098: 4C                   orl     a,      r4
0099: FA                   mov     r2,     a
009A: ED                   mov     a,      r5
009B: 54 0F                anl     a,      #0x0f
                   
009D: FC                   mov     r4,     a
009E: EB                   mov     a,      r3
009F: C4                   swap    a
00A0: FD                   mov     r5,     a
00A1: 54 F0                anl     a,      #0xf0
00A3: 4C                   orl     a,      r4
00A4: FB                   mov     r3,     a
00A5: ED                   mov     a,      r5
00A6: 54 0F                anl     a,      #0x0f
                   
00A8: 70 02                jnz     read_hex_32__end        ; overflow
00AA: 80 B7                sjmp    read_hex_32__loop
                   
                   
                   read_hex_32__end:
00AC: D0 E0                pop     acc
00AE: FD                   mov     r5,     a
00AF: D0 E0                pop     acc
00B1: FC                   mov     r4,     a
00B2: D0 E0                pop     acc
00B4: 22                   ret
                   
                   ; *** print/char.inc ***
                   
                   ; >> void print_char(char a) <<
                   
                   ; Print char from accumulator.
                   ; Indirect call to function behind OUT pointer.
                   
                   print_char:
00B5: C0 32                push    OUT
00B7: C0 33                push    OUT+1
00B9: 22                   ret
                   
                   ; *** print/hex.inc ***
                   
                   ; >> void print_hex_8(char a) <<
                   
                   ; Print hexadecimal number from accumulator.
                   
                   print_hex_8:
00BA: C4                   swap    a       ; high nibble first
00BB: 11 BE                acall   print_hex_8__nibble
00BD: C4                   swap    a
                   print_hex_8__nibble:
00BE: C0 E0                push    ACC
00C0: 54 0F                anl     a,      #0x0f
00C2: 24 90                add     a,      #0x90
00C4: D4                   da      a
00C5: 34 40                addc    a,      #0x40
00C7: D4                   da      a
00C8: 44 20                orl     a,      #0x20   ; lower case
00CA: 11 B5                acall   print_char
00CC: D0 E0                pop     ACC
00CE: 22                   ret
                   
                   ; >> void print_hex_16(int r0r1) <<
                   
                   ; Print hexadecimal number from r0-r1.
                   
                   print_hex_16:
00CF: C0 E0                push    ACC
00D1: E9                   mov     a,      r1      ; MSB first
00D2: 11 BA                acall   print_hex_8
00D4: E8                   mov     a,      r0      ; LSB second
00D5: 11 BA                acall   print_hex_8
00D7: D0 E0                pop     ACC
00D9: 22                   ret
                   
                   ; >> void print_hex_32(long r0r1r2r3) <<
                   
                   ; Print hexadecimal number from r0-r3.
                   
                   print_hex_32:
00DA: C0 E0                push    ACC
00DC: EB                   mov     a,      r3      ; MSB first
00DD: 11 BA                acall   print_hex_8
00DF: EA                   mov     a,      r2
00E0: 11 BA                acall   print_hex_8
00E2: E9                   mov     a,      r1
00E3: 11 BA                acall   print_hex_8
00E5: E8                   mov     a,      r0      ; LSB last
00E6: 11 BA                acall   print_hex_8
00E8: D0 E0                pop     ACC
00EA: 22                   ret
