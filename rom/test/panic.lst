                   
                   ; *******************************
                   ; * Panic function Test Program *
                   ; *******************************
                   
                   ; This program triggers the "panic" function,
                   ; which handles unrecoverable errors
                   ; by printng the address from where it was called
                   ; and halting the system.
                   
                   ; **********************
                   ; * Header Definitions *
                   ; **********************
                   
                   ; *** global/variables.equ ***
                   
                   ; variables in internal RAM
                   
                   ; 0x00-0x1f: register banks 0-3
                   ; 0x20-0x2f: bit addressable variables
                   
0000:              .equ    in,             0x30    ; pointer to input handling 
0000:              .equ    out,            0x32    ; pointer to ouput handling 
                   
0000:              .equ    stream_in,      0x34    ; input stream position
0000:              .equ    stream_out,     0x36    ; output stream position
                   
0000:              .equ    memory_list,    0x38    ; pointer to heap list head
0000:              .equ    memory_start,   0x40    ; first allocatable address
0000:              .equ    memory_end,     0x42    ; last allocatable address
                   
0000:              .equ    block_read,     0x44    ; pointers to block device d
0000:              .equ    block_write,    0x46    ; code (read and write funct
0000:              .equ    block_cache,    0x48    ; pointer to block cache dat
                   
0000:              .equ    fatfs_info,     0x50    ; pointer to FAT file system
                   
0000:              .equ    stack,          0x60    ; beginning of stack (grows 
                   
                   ; *** serial/sfr.equ ***
                   
                   ; Special function registers for serial interface.
                   ; See chapter 14 of AT89C51ED2 datasheet, page 48ff.
                   
                   ; Internal Baud Rate Generator (BRG), see datasheet page 52.
0000:                      .equ    BRL,    0x9A    ; baud rate reload value
0000:                      .equ    BDRCON, 0x9B    ; BRG control register
                   
                   ; *************
                   ; * Main Code *
                   ; *************
                   
0000:                      .org    0
                   
                   
                   ; *** global/init.inc ***
                   
                   ; Initialize stack pointer and global variables.
                   ; This must be included early in the main program.
                   
                   global_init:
0000: 75 81 60             mov     SP,#STACK
                   
                   ; Select register bank zero, clear processor flags
0003: 75 D0 00             mov     PSW,#0
                   
                   ; Fill internal RAM with markers for uninitialized data
0006: 78 00                mov     r0,#0 ; write pointer
                   
0008: 74 3F                mov     a,#0x3f ; '?' for uninitialized registers
                   global_init__1:
000A: 08                   inc     r0 ; first iteration skips address 0, r0 liv
000B: F6                   mov     @r0,a
000C: B8 1F FB             cjne    r0,#0x1f,global_init__1
                   
000F: 74 2A                mov     a,#0x2a ; '*' for uninitialized variables
                   global_init__2:
0011: 08                   inc     r0
0012: F6                   mov     @r0,a
0013: B8 5F FB             cjne    r0,#STACK-1,global_init__2
                   
0016: 74 7E                mov     a,#0x7e ; '~' for stack and above
                   global_init__3:
0018: 08                   inc     r0
0019: F6                   mov     @r0,a
001A: B8 FF FB             cjne    r0,#0xff,global_init__3
                   
                   ; *** test/panic.asm ***
                   
001D: 11 21                acall   serial_init
                   
001F: 11 99                acall   panic
                   
                   ; *********************
                   ; * Library Functions *
                   ; *********************
                   
                   ; *** serial/init.inc ***
                   
                   ; >> void serial_init() <<
                   
                   ; Initialize serial interface.
                   ; - assumes 20MHz clock and X2 mode
                   ; - uses internal baud rate generator
                   ; - speed: 9600 bits per second
                   ; Formula: BRL = 256 - (F_clk / (32 * Baud_Rate))
                   ; ---
                   ; See datasheet page 52 for details,
                   ; also note oscillator diagram on page 16!
                   
                   serial_init:
0021: 75 98 52             mov     SCON,   #0x52   ; SM1, REN, TI
0024: 75 9A BF             mov     BRL,    #191    ; 9600 @20MHz CLK
0027: 75 9B 1E             mov     BDRCON, #0x1e   ; BRR, RBCK, TBCK, SPD
                   
002A: 90 00 3D             mov     dptr,   #serial_rx
002D: 85 82 30             mov     IN,     DPL
0030: 85 83 31             mov     IN+1,   DPH
                   
0033: 90 00 45             mov     dptr,   #serial_tx
0036: 85 82 32             mov     OUT,    DPL
0039: 85 83 33             mov     OUT+1,  DPH
                   
003C: 22                   ret
                   
                   ; *** serial/rx.inc ***
                   
                   ; >> char serial_rx <<
                   
                   ; Receive byte into accumulator.
                   
                   serial_rx:
003D: 30 98 FD             jnb     RI,     serial_rx
0040: C2 98                clr     RI
0042: E5 99                mov     a,      SBUF
0044: 22                   ret
                   
                   ; *** serial/tx.inc ***
                   
                   ; >> void serial_tx(char a) <<
                   
                   ; Transmit byte from accumulator.
                   
                   serial_tx:
0045: 30 99 FD             jnb     TI,     serial_tx
0048: C2 99                clr     TI
004A: F5 99                mov     SBUF,   a
004C: 22                   ret
                   
                   ; *** print/char.inc ***
                   
                   ; >> void print_char(char a) <<
                   
                   ; Print char from accumulator.
                   ; Indirect call to function behind OUT pointer.
                   
                   print_char:
004D: C0 32                push    OUT
004F: C0 33                push    OUT+1
0051: 22                   ret
                   
                   ; *** print/text.inc ***
                   
                   ; >> void print_text(char* dptr) <<
                   
                   ; Print null-terminated string from program code.
                   ; Aborts if no null terminator is found after 256 bytes.
                   
                   print_text:
0052: C0 E0                push    ACC
0054: E8                   mov     a,      r0
0055: C0 E0                push    ACC
                   
0057: E4                   clr     a
0058: F8                   mov     r0,     a
                   print_text__loop:
0059: 93                   movc    a,      @dptr+a
005A: 60 06                jz      print_text__end
005C: 11 4D                acall   print_char
005E: 08                   inc     r0
005F: E8                   mov     a,      r0
0060: 70 F7                jnz     print_text__loop
                   
                   print_text__end:
0062: D0 E0                pop     ACC
0064: F8                   mov     r0,     a
0065: D0 E0                pop     ACC
0067: 22                   ret
                   
                   ; *** print/hex.inc ***
                   
                   ; >> void print_hex_8(char a) <<
                   
                   ; Print hexadecimal number from accumulator.
                   
                   print_hex_8:
0068: C4                   swap    a       ; high nibble first
0069: 11 6C                acall   print_hex_8__nibble
006B: C4                   swap    a
                   print_hex_8__nibble:
006C: C0 E0                push    ACC
006E: 54 0F                anl     a,      #0x0f
0070: 24 90                add     a,      #0x90
0072: D4                   da      a
0073: 34 40                addc    a,      #0x40
0075: D4                   da      a
0076: 44 20                orl     a,      #0x20   ; lower case
0078: 11 4D                acall   print_char
007A: D0 E0                pop     ACC
007C: 22                   ret
                   
                   ; >> void print_hex_16(int r0r1) <<
                   
                   ; Print hexadecimal number from r0-r1.
                   
                   print_hex_16:
007D: C0 E0                push    ACC
007F: E9                   mov     a,      r1      ; MSB first
0080: 11 68                acall   print_hex_8
0082: E8                   mov     a,      r0      ; LSB second
0083: 11 68                acall   print_hex_8
0085: D0 E0                pop     ACC
0087: 22                   ret
                   
                   ; >> void print_hex_32(long r0r1r2r3) <<
                   
                   ; Print hexadecimal number from r0-r3.
                   
                   print_hex_32:
0088: C0 E0                push    ACC
008A: EB                   mov     a,      r3      ; MSB first
008B: 11 68                acall   print_hex_8
008D: EA                   mov     a,      r2
008E: 11 68                acall   print_hex_8
0090: E9                   mov     a,      r1
0091: 11 68                acall   print_hex_8
0093: E8                   mov     a,      r0      ; LSB last
0094: 11 68                acall   print_hex_8
0096: D0 E0                pop     ACC
0098: 22                   ret
                   
0099:              .equ panic_out, serial_tx
                   
                   ; *** panic/panic.inc ***
                   
                   ; >> void panic() <<
                   
                   ; Unrecoverable error handler.
                   ; Print caller address and halt.
                   
                   panic:
0099: C2 AF                clr     ea      ; disable interrupts
009B: F8                   mov     r0,a    ; save a
                           ; display panic text
009C: 90 00 45             mov     dptr,#panic_out
009F: 85 82 32             mov     out,dpl
00A2: 85 83 33             mov     out+1,dph
00A5: 90 00 CC             mov     dptr,#panic_text
00A8: 11 52                acall   print_text
                           ; get return address from stack
00AA: D0 E0                pop     acc
00AC: FA                   mov     r2,a
00AD: D0 E0                pop     acc
                           ; subtract two to get call instruction address
00AF: C3                   clr     c
00B0: 94 02                subb    a,#2
00B2: F9                   mov     r1,a
00B3: EA                   mov     a,r2
00B4: 94 00                subb    a,#0
                           ; print caller address (to look up cause in .lst fil
00B6: 11 68                acall   print_hex_8
00B8: E9                   mov     a,r1
00B9: 11 68                acall   print_hex_8
                           ; print original value of a (error code / debug info
00BB: 74 20                mov     a,#' '
00BD: 11 4D                acall   print_char
00BF: E8                   mov     a,r0
00C0: 11 68                acall   print_hex_8
                           ; newline
00C2: 74 0D                mov     a,#13
00C4: 11 4D                acall   print_char
00C6: 74 0A                mov     a,#10
00C8: 11 4D                acall   print_char
                           ; hang
00CA: 80 FE                sjmp    *
                   
                   panic_text:
00CC: 0D 0A                .db     13, 10
00CE: 20 21 21 21 
      20 70 61 6E 
      69 63 20 40 
                           .db     " !!! panic @"
00DA: 00                   .db     0
